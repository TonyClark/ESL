/********************************************************************************************************************************
 *                                                                                                                              *
 *                                                 The ESL Compiler                                                             *
 *                                                 ================                                                             *
 *                                                                                                                              *
 *   The ESL compiler performs type-checking, case-expression simplification, variable analysis, and translation to Java.       *
 *                                                                                                                              *
 *   (a) Type checking is performed on ESL source to check operations and messages are correctly performed.                     *
 *   (b) Case expressions are simplified to allow them to be represented as switch and if in Java.                              *
 *   (c) Variable analysis is performed to ensure that lambda-bound variables can be updated in Java.                           *
 *   (d) ESL AST is translated to Java AST. The Java AST is translated to a String and written to a file.                       *
 *                                                                                                                              *
 *   The target Java code is written to esl.* and relies on library code in the classes esl.Lib and esl.ESLVal.                 *
 *                                                                                                                              *
 ********************************************************************************************************************************/

export main, compileFile, runFile, compileModule;

import
  'esl/compiler/ppExp.esl', 
  'esl/lists.esl', 
  'esl/compiler/types.esl', 
  'esl/compiler/typeCheck.esl', 
  'esl/compiler/toJava.esl',
  'esl/displays.esl',
  'esl/compiler/strings.esl',
  'esl/compiler/warnings.esl',
  'esl/compiler/methodTransform.esl';

type Main = Act { Time(Int); }

moduleImports(m::AST)::[Str] = 
  case m {
    Module(path::Str,name::Str,exports::[Str],imports::[Str],decs::[TDec],y::Void,defs::[TBind]) ->  
      imports;
  }

compileFile(sourceFile::Str)::Void =
  let path::[Str] = toPath(sourceFile); in 
  let className::Str = pathToJavaClassName(path); in
  let sourcePath::Str = 'src/' + joinBy(#/,butlast[Str](path)) + '/' + className + '.java'; in
  let package1::Str = pathToJavaPackage(path);
  in { 
    if sourcePath.writeDate < sourceFile.writeDate
    then compileModule(sourcePath,package1,className,sourceFile);
    else print[Str]('[ ' + sourceFile + ' is up to date. ]');
  }

runFile(sourceFile::Str)::Void =
  let path::[Str] = toPath(sourceFile); in
  let className::Str = pathToJavaClassName(path); in
  let sourcePath::Str = 'src/' + joinBy(#/,butlast[Str](path)) + '/' + className + '.java'; in
  let package1::Str = pathToJavaPackage(path);
  in runModule(sourcePath,package1,className,sourceFile);

compileModule(sourcePath::Str,package1::Str,name::Str,path::Str)::Void = {
  print[Str]('[ compile ' + sourcePath + ']');
  try {
    let module::AST = parse[AST](path); in
    let imports::[Str] = moduleImports(module);
    in {
      resetWarnings();
      typeCheckModule(path);
      let untypedJModule::JMod = addMethodCalls(moduleToJava(module)); 
      in { 
        edb <- Edit(JavaSource(sourcePath,name,ppJModule(name,package1,untypedJModule)));  
        for i::Str in imports do {
          compileFile(i); 
        }
      }
    }
  } catch {
      message::Str ->  print[Str]('Type Error: ' + message); 
  }
}

runModule(sourcePath::Str,package1::Str,name::Str,path::Str)::Void = { 
  print[Str]('[ compile ' + sourcePath + ']');
  try {
    let module::AST = parse[AST](path); in
    let imports::[Str] = moduleImports(module);
    in {
      resetWarnings();
      typeCheckModule(path);
      let untypedJModule::JMod = addMethodCalls(moduleToJava(module)); 
      in { 
        edb <- RunJava(sourcePath,name,ppJModule(name,package1,untypedJModule));  
        for i::Str in imports do {
          compileFile(i); 
        }
      }
    }
  } catch {
      message::Str ->  print[Str]('Type Error: ' + message); 
  }
}
  
act main::Main { 
  Time(t::Int) -> {
    stopAll();
  }
}